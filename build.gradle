plugins {
    id 'java'
    id 'io.github.goooler.shadow' version '8.1.8'
    id("xyz.jpenilla.run-paper") version "2.3.1"
}

group = 'gg.corn'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        url = 'https://repo.extendedclip.com/releases/'
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.10-R0.1-SNAPSHOT")
    compileOnly 'me.clip:placeholderapi:2.11.7'
    implementation 'com.zaxxer:HikariCP:5.1.0'
    implementation 'org.slf4j:slf4j-api:2.0.9'
    implementation 'org.slf4j:slf4j-jdk14:2.0.9' // Bridge SLF4J to Java Util Logging
    implementation 'com.h2database:h2:2.2.224'
}

// Configure shadow plugin to relocate HikariCP
shadowJar {
    archiveClassifier.set('')
    relocate 'com.zaxxer.hikari', 'gg.corn.DunceChat.libs.hikari'
    relocate 'org.slf4j', 'gg.corn.DunceChat.libs.slf4j'
    // Note: H2 is NOT relocated because JDBC DriverManager needs to find it via SPI

    // Merge service files to ensure JDBC drivers are registered
    mergeServiceFiles()
}

// Use shadowJar as the default build artifact
build {
    dependsOn shadowJar
}

// Make the jar task depend on shadowJar
jar {
    enabled = false
}

tasks {
  runServer {
    // Configure the Minecraft version for our task.
    // This is the only required configuration besides applying the plugin.
    // Your plugin's jar (or shadowJar if present) will be used automatically.
    minecraftVersion("1.21")
  }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
